plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.6'
	id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id 'org.sonarqube' version '5.1.0.4882'
    id 'checkstyle'
    id 'pmd'
}

group = 'com.careerpath'
version = '0.0.1-SNAPSHOT'
description = 'Backend for IP - CareerPath'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integrationTest/java')
        resources.srcDir file('src/integrationTest/resources')

        compileClasspath += sourceSets.main.output + sourceSets.test.output + configurations.testRuntimeClasspath
        runtimeClasspath += sourceSets.main.output + sourceSets.test.output + output + configurations.testRuntimeClasspath
    }
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    useJUnitPlatform()
    shouldRunAfter test
}

tasks.named('integrationTest') {
    systemProperty "spring.profiles.active", "test"
    useJUnitPlatform()
}

tasks.processIntegrationTestResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

// check.dependsOn integrationTest

repositories {
	mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.ai:spring-ai-bom:1.0.0"
    }
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.vladmihalcea:hibernate-types-60:2.21.1'
    implementation "org.springframework.boot:spring-boot-starter-webflux"
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'com.theokanning.openai-gpt3-java:service:0.18.2'
    compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:postgresql'
    testImplementation "org.springframework.boot:spring-boot-testcontainers"
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	implementation 'jakarta.persistence:jakarta.persistence-api:3.1.0'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.flywaydb:flyway-core:11.11.0'
    implementation 'org.flywaydb:flyway-database-postgresql:11.11.0'
    implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
    implementation 'io.jsonwebtoken:jjwt-impl:0.12.5'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.12.5'
    implementation 'org.springframework.boot:spring-boot-starter-security'
}

tasks.named('test') {
	useJUnitPlatform()
    systemProperty "spring.profiles.active", "test"
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.10"
}

test {
    systemProperty "spring.profiles.active", "test"
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

// Unit test coverage
jacocoTestReport {
    dependsOn test
    executionData(fileTree(buildDir).include("jacoco/test.exec*"))
    reports {
        xml.required.set(true)
        html.required.set(true)
    }
}

// Integration coverage
tasks.register('jacocoIntegrationTestReport', JacocoReport) {
    dependsOn integrationTest
    executionData(fileTree(buildDir).include("jacoco/integrationTest.exec*"))
    sourceSets sourceSets.main
    reports {
        xml.required.set(true)
        html.required.set(true)
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "careerpath-backend"
        property "sonar.projectName", "CareerPath Backend"
        property "sonar.host.url", System.getenv("SONAR_HOST_URL")
        property "sonar.login", System.getenv("SONAR_BACKEND_TOKEN")

        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"

        property "sonar.sources", "src/main/java"
        property "sonar.tests", "src/test/java"
        property "sonar.junit.reportPaths", "${project.buildDir}/test-results/test"
    }
}

checkstyle {
    toolVersion = "10.17.0"
    configFile = file("config/checkstyle/checkstyle.xml")
    ignoreFailures = false
    showViolations = true
}

tasks.withType(Checkstyle).configureEach {
    reports {
        xml.required.set(true)
        html.required.set(true)
    }
}

pmd {
    toolVersion = "7.1.0"
    ignoreFailures = false
    ruleSets = []
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

tasks.withType(Pmd).configureEach {
    reports {
        xml.required.set(true)
        html.required.set(true)
    }
}

check {
    dependsOn test
}

tasks.named('check') {
    dependsOn integrationTest
}


